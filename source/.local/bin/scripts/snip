#!/bin/zsh
# -----------------------------------------------------------------------------
# Snip
#
# Wrapper script that handles the generic usecase of taking screenshots.
# By default, it captures the whole screen and saves it in the designated
# screenshot directory.  Also displays a notification if successful.
#
# Available arguments:
# -c : Copies the image to clipboard rather than saving it to disk.
# -s : Allows the user to select a specific region.
# -----------------------------------------------------------------------------

# Parse arguments.
while getopts "cs" OPTION; do
  case $OPTION in
  c) CLIPBOARD=1 ;;
  s) SELECTION=1 ;;
  *) exit 1 ;;
  esac
done

# Set the common command args.
OPTS=("-u" "--quality=10" "--format=png")

# If using a selection, add some arguments.
[[ -v SELECTION ]] && OPTS+=("-slq" "--color=0.19,0.27,0.28,0.25")

# Take the screenshot and save it to desired location.
if [[ -v CLIPBOARD ]]; then
  maim $^OPTS | xclip -selection clipboard -t image/png
  # shellcheck disable=SC2154
  [ "${pipestatus[1]}" = 0 ] && notify-send "Screenshot saved" "</i>Clipboard<i>"
else
  FILE="$(xdg-user-dir SCREENSHOTS)/screen-$(date +%s).png"
  maim $^OPTS "$FILE" && notify-send "Screenshot saved" "<i>$FILE</i>"
fi
